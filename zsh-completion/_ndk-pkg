#compdef ndk-pkg

_commands=(
    '-h:show help of this command.'
    '--help:show help of this command.'
    '-V:show version of this.'
    '--version:show version of this command.'
    '--homedir:show home directory of this command.'
    '--homepage:show home webpage of this command.'
    'search:search packages can be installed.'
    'ls-target-abis:list the supported target abis.'
    'ls-target-archs:list the supported target archs.'
    'ls-target-levels:list the supported android-sdk api-levels.'
    'ls-target-triples:list the supported target triples.'
    'ls-available:list the available packages.'
    'ls-installed:list the installed packages.'
    'ls-outdated:list the installed packages which can be upgraded.'
    'is-available:is the given package available.'
    'is-installed:is the given package installed.'
    'is-outdated:is the given package outdated.'
    'tree:list files of the given installed package in a tree-like format.'
    'info:show infomation of the given package or all available packages.'
    'formula:create, delete, rename, view, edit the formula of the given package.'
    'formula-repo:add, delete, list the formula-repos.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:cleanup the unused cache.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages or this software.'
    'fetch:download formula resources of the given package to the cache.'
    'homepage:show or open the homepage of the given package or the this project.'
    'integrate:integrate zsh completions and cmake support.'
    'env:show current machine os and android-ndk toolchain info.'
    'depends:show the depends of the given package.'
    'pack:pack the given installed package.'
    'deploy:pack the given installed package as prefab aar and then deploy the prefab aar to sonatype nexus OSSRH.'
    'logs:show logs of the given installed package.'
    'ldd:list dynamic dependencies.'
    'gen-github-workflows:generate github workflows.'
    'ndkmanager:install android-ndk to ~/.ndk-pkg.'
    'compare-versions:compare package versions with brew or maven central.'
)

function _ndk_pkg_commands() {
    _describe 'command' _commands
}

function _ndk_pkg_available_packages() {
    local -a _available_packages
    _available_packages=($(ndk-pkg ls-available 2>/dev/null))
    _describe 'available-packages' _available_packages
}

function _ndk_pkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(ndk-pkg ls-installed 2>/dev/null | cut -d/ -f3))
    _describe 'installed-packages' _installed_packages
}

function _ndk_pkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(ndk-pkg ls-outdated 2>/dev/null | cut -d/ -f3))
    _describe 'outdated-packages' _outdated_packages
}

function _ndk_pkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :_ndk_pkg_commands' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                ls-installed)
                    _arguments '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))'
                    ;;
                ls-outdated)
                    _arguments '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))'
                    ;;
                compare-versions)
                    _arguments ':which:(brew maven)'
                    ;;
                ndkmanager)
                    _arguments \
                        ':action:(ls-available install)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                ls-available) _arguments '--only-rversion[only show rversion]' ;;
                                install)      _arguments '1:rversion:($(ndk-pkg ndkmanager ls-available --only-rversion))' ;;
                            esac
                    esac
                    ;;
                formula-repo)
                    _arguments \
                        ':action:(add del list)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                del) _arguments '1:repo-name:($(ndk-pkg formula-repo list | cut -d= -f1))' ;;
                            esac
                    esac
                    ;;
                formula)
                    _arguments \
                        ':action:(create delete rename view edit list)' \
                        '*:: :->action'
                    case $state in
                        action)
                            case $words[1] in
                                delete|rename|edit)
                                    _arguments '1:package-name:_ndk_pkg_available_packages'
                                    ;;
                                view)
                                    _arguments \
                                        '1:package-name:_ndk_pkg_available_packages' \
                                        '--paging=-[when use pager]:paging:(auto always never)'
                                    ;;
                            esac
                    esac
                    ;;
                integrate)
                    _arguments \
                        '1:what:(zsh cmake)' \
                        '--output-dir=-[set the output dir]:output-dir:{_files -/}' \
                        '-x[trace execution of commands]' \
                        '--china[use china mirror url]'
                    ;;
                info)
                    if [ "$words[2]" = '@all' ] ; then
                        _arguments \
                            '1:package-name:(@all)' \
                            '2:output-format:(--json --yaml --shell)'
                    else
                        case $words[3] in
                            installed-*)
                                _arguments \
                                    '1:package-name:()' \
                                    '2:key:(installed-dir installed-metadata installed-datetime-unix installed-datetime-formatted installed-pkg-version installed-files installed-abis)' \
                                    '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))'
                                ;;
                            *)  _arguments \
                                    '1:package-name:(@all $(ndk-pkg ls-available 2>/dev/null))' \
                                    '2:key:(formula summary webpage version license git.url git.sha src.url src.sha dep.cmd dep.pkg patches cdefine ccflags xxflags ldflags sourced binsrcd bsystem installed-dir installed-metadata installed-datetime-unix installed-datetime-formatted installed-pkg-version installed-files installed-abis --json --yaml --shell)'
                        esac
                    fi
                    ;;
                fetch)
                    _arguments ':package-name:(@all $(ndk-pkg ls-available 2>/dev/null))'
                    ;;
                depends)
                    _arguments \
                        '1:package-name:_ndk_pkg_available_packages' \
                        '--format=-[set output format]:format:(list json dot txt png)' \
                        '--brief[output breif]' \
                        '-o[set output file path]:output-filepath:{_files -/}'
                    ;;
                pack)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '3:type:(7z zip aar tar.gz tar.xz tar.bz2)' \
                        '--keep-packing-dir[do not delete packing dir even if packed success]'
                    ;;
                deploy)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '3:config-file:_files' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]' \
                        '(--debug -d)'{--debug,-d}'[debug the process of deployment]'
                    ;;
                homepage)
                    _arguments \
                        ':package-name:_ndk_pkg_available_packages' \
                        '--open[open in the web browser]'
                    ;;
                tree)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '-L[level]:level:(3)' \
                        '--dirsfirst[show dirs first]'
                    ;;
                logs)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '3:abi:($(ndk-pkg ls-target-abis))'
                    ;;
                install)
                    _arguments \
                        ':package-name:(@all $(ndk-pkg ls-available 2>/dev/null))' \
                        '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]' \
                        '(--debug -d)'{--debug,-d}'[debug output]' \
                        '(--verbose -v)'{--verbose,-v}'[verbose mode. many messages will be output to terminal]' \
                        '(--quiet -q)'{--quiet,-q}'[silent mode. no any messages will be output to terminal]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                        '--install-exe[install executable programs]' \
                        '--install-doc[install documents]' \
                        '--install-man[install manpages]' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst)' \
                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_ndk_pkg_installed_packages' \
                        '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]' \
                        '(--debug -d)'{--debug,-d}'[debug output]' \
                        '(--verbose -v)'{--verbose,-v}'[verbose mode. many messages will be output to terminal]' \
                        '(--quiet -q)'{--quiet,-q}'[silent mode. no any messages will be output to terminal]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                        '--install-exe[install executable programs]' \
                        '--install-doc[install documents]' \
                        '--install-man[install manpages]' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst)' \
                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)'
                    ;;
                upgrade)
                    _arguments \
                        '1:package-name:(@self $(ndk-pkg ls-outdated))' \
                        '*:: :->upgrade-subcommand'
                    case $state in
                        upgrade-subcommand)
                            case $words[1] in
                                @self)
                                    _arguments \
                                        '-x[trace execution of commands]' \
                                        '--china[use china mirror url]'
                                    ;;
                                *)  _arguments \
                                        ':package-name:_ndk_pkg_outdated_packages' \
                                        '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                                        '--dry-run[dry-run]' \
                                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                                        '(--xtrace -x)'{--xtrace,-x}'[trace execution of commands]' \
                                        '(--debug -d)'{--debug,-d}'[debug output]' \
                                        '(--verbose -v)'{--verbose,-v}'[verbose mode. many messages will be output to terminal]' \
                                        '(--quiet -q)'{--quiet,-q}'[silent mode. no any messages will be output to terminal]' \
                                        '--build-type=-[set build type]:build-type:(debug release)' \
                                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                                        '--install-exe[install executable programs]' \
                                        '--install-doc[install documents]' \
                                        '--install-man[install manpages]' \
                                        '--export-compile-commands-json[generates compile_commands.json]' \
                                        '--disable-ccache[do not use ccache]' \
                                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst)' \
                                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)'
                                    ;;
                            esac
                        esac
                    ;;
                uninstall)
                    _arguments \
                        '*:package-name:_ndk_pkg_installed_packages' \
                        '--min-sdk-api-level=-[set the min android-sdk api-level]:min-sdk-api-level:($(ndk-pkg ls-target-levels))' \
                        ;;
                is-available)
                    _arguments \
                        '1:package-name:_ndk_pkg_available_packages' \
                        '2:condition-operator:(eq ne ge gt le lt)'
                    ;;
                is-installed)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))'
                        ;;
                is-outdated)
                    _arguments \
                        '1:package-name:_ndk_pkg_outdated_packages' \
                        '2:min-sdk-api-level:($(ndk-pkg ls-target-levels))'
                        ;;
                ldd)
                    _arguments \
                        '1:package-name:_ndk_pkg_installed_packages' \
                        '2:abi:($(ndk-pkg ls-target-abis))' \
                        '3:type:(bin lib)'
                    ;;
                gen-github-workflows)
                    _arguments \
                        '--group-size=-[set the group size]:group-size:(50 40 30 20)' \
                        '--output-dir=-[set the output dir]:output-dir:{_files -/}'
                    ;;
                *);;
            esac
            ;;
        *);;
    esac
}

_ndk_pkg "$@"
