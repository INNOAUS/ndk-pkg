#compdef ndk-pkg

_commands=(
    '-h:print the help infomation of ndk-pkg command.'
    '--help:print the help infomation of ndk-pkg command.'
    'help:print the help infomation of ndk-pkg command.'
    '-V:print the version of ndk-pkg and NDK.'
    '--version:print the version of ndk-pkg and NDK.'
    'version:print the version of ndk-pkg and NDK.'
    'search:search packages can be installed.'
    'list:list the target-abis, target-apis, available, installed and outdated packages.'
    'tree:list contents of a installed package directory in a tree-like format.'
    'info:print the basic infomation of packages.'
    'cat:view the formula source code of a package.'
    'edit:edit the formula source code of a package.'
    'env:print the environment variable settings.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:cleanup the unused cache.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'fetch:download formula resources of a package to the cache.'
    'homepage:visit the homepage of a formula or the ndk-pkg project.'
    'prefix:show the installation direcotory of a formula or the ndk-pkg home.'
    'pack:pack a installed package.'
    'logs:print the logs of a installed package.'
    'is:check something. easy to write script.'
)

_list=(
    'target-abis:list support target abis.'
    'target-apis:list support target api levels.'
    'available:list the available packages.'
    'installed:list the installed packages.'
    'outdated:list the installed packages which can be upgraded.'
)

function _ndk_pkg_list() {
    _describe 'list' _list
}

function _ndk_pkg_commands() {
    _describe 'command' _commands
}

function _ndk_pkg_available_packages() {
    local -a _available_packages
    _available_packages=($(ndk-pkg list available))
    _describe 'available-packages' _available_packages
}

function _ndk_pkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(ndk-pkg list installed))
    _describe 'installed-packages' _installed_packages
}

function _ndk_pkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(ndk-pkg list outdated))
    _describe 'outdated-packages' _outdated_packages
}

function _ndk_pkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :_ndk_pkg_commands' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                list)
                    _arguments ':list:_ndk_pkg_list' ;;
                tree)
                    _arguments ':package-name:_ndk_pkg_available_packages' ;;
                info)
                    _arguments '*:package-name:_ndk_pkg_available_packages' ;;
                cat|edit|fetch|homepage)
                    _arguments ':package-name:_ndk_pkg_available_packages' ;;
                prefix|logs|pack)
                    _arguments ':package-name:_ndk_pkg_installed_packages' ;;
                env)
                    _arguments \
                        ':package-name:_ndk_pkg_available_packages' \
                        '--target-api=-[set target api]:target-api:($(ndk-pkg list target-apis))' \
                        '--target-abi=-[set target abi]:target-abi:($(ndk-pkg list target-abis))' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]'
                    ;;
                install)
                    _arguments \
                        ':package-name:_ndk_pkg_available_packages' \
                        '--target-api=-[set target api]:target-api:($(ndk-pkg list target-apis))' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_ndk_pkg_installed_packages' \
                        '--target-api=-[set target api]:target-api:($(ndk-pkg list target-apis))' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_ndk_pkg_outdated_packages' \
                        '--target-api=-[set target api]:target-api:($(ndk-pkg list target-apis))' \
                        '(--verbose -v)'{--verbose,-v}'[verbose output log]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_ndk_pkg_installed_packages' ;;
                is)
                    _arguments \
                        '1: :(available installed outdated)' \
                        '2:package-name:_ndk_pkg_available_packages'
                    ;;
                *);;
            esac
            ;;
        *);;
    esac
}

_ndk_pkg "$@"
